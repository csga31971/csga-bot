group 'com.moebuff.discord'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "eclipse"
apply plugin: "idea"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceSets.main {
    java.srcDirs = ["src/"]
    resources.srcDirs = ["res/"]
}

project.ext.mainClassName = "com.moebuff.discord.BotLauncher"
project.ext.assetsDir = new File("assets")

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }
    from files(project.assetsDir) { exclude '.csga' }
    exclude 'log4j2-test.xml'

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "http://www.beatunes.com/repo/maven2" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile "commons-codec:commons-codec:1.10"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "commons-io:commons-io:2.5"
    compile "commons-beanutils:commons-beanutils:1.9.3"
    compileOnly "org.projectlombok:lombok:1.16.12"
    compile "org.slf4j:slf4j-api:1.7.21"
    compile "org.apache.logging.log4j:log4j-api:2.7"
    compile "org.apache.logging.log4j:log4j-core:2.7"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.7"
    compile "com.lmax:disruptor:3.3.6"
}
